prototype(Sitegeist.PaperTiger:Form) < prototype(Neos.Neos:ContentComponent) {

    class = ''

    backendStyle = ${StaticResource.content('Sitegeist.PaperTiger', 'Public/Styles/Backend.css')}

    # we put class into the context to make in available insode the runtime form
    @context._formClass = ${this.class}

    renderer = Neos.Fusion:Case {
        runtimeForm {
            condition = ${node.context.live}
            renderer = Neos.Fusion.Form:Runtime.RuntimeForm {
                @cache.context.formClass = '_formClass'
                namespace = ${q(node).property('_identifier')}
                attributes.class = ${_formClass}
                process = Neos.Fusion.Form:Runtime.SingleStepProcess {
                    content = Sitegeist.PaperTiger:FieldCollection.Renderer {
                        nodePath = 'fields'
                    }
                    schema = Sitegeist.PaperTiger:FieldCollection.Schema {
                        nodePath = 'fields'
                    }
                    footer >
                }
                action = Sitegeist.PaperTiger:ActionCollection.Definition {
                    nodePath = 'actions'
                }
            }
        }

        backend {
            condition = ${node.context.inBackend}
            renderer = afx`
                <style @if={props.backendStyle} data-slipstream>{props.backendStyle}</style>
                <div class="papertiger-form">
                    <div class="papertiger-form__header">
                        <span class="paper-tiger-form__header-number">1</span>
                        <span class="paper-tiger-form__header-title">Form fields</span>
                    </div>
                    <form class={_formClass}>
                        <Sitegeist.PaperTiger:FieldCollection.Renderer nodePath="fields"/>
                    </form>
                    <div class="papertiger-form__header">
                        <span class="paper-tiger-form__header-number">2</span>
                        <span class="paper-tiger-form__header-title">Follow up actions</span>
                    </div>
                    <Sitegeist.PaperTiger:ActionCollection.Preview nodePath="actions"/>
                </div>
            `
        }
    }
}

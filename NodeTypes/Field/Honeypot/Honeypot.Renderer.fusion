prototype(Sitegeist.PaperTiger:Field.Honeypot.Renderer) < prototype(Neos.Fusion:Component) {

  identifier = ${q(node).property('identifier')}

  renderer = Neos.Fusion:Case {
    backend {
      condition = ${node.context.inBackend}
      renderer = afx`
        <Sitegeist.PaperTiger:FieldContainerRenderer
          field.name={props.identifier}
        >
          <label>Honeypot</label> {props.identifier}
        </Sitegeist.PaperTiger:FieldContainerRenderer>
      `
    }

    default {
      condition = true
      renderer = afx`
        <Neos.Fusion.Form:FieldContainer
          field.name={props.identifier}
          attributes.style="display:none !important;"
          attributes.autocomplete="off"
          attributes.tabindex="-1"
        >
          <Neos.Fusion.Form:Input />
        </Neos.Fusion.Form:FieldContainer>
      `
    }
  }
}

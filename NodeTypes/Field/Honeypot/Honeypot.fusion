prototype(Sitegeist.PaperTiger:Field.Honeypot) < prototype(Neos.Neos:ContentComponent) {
    name = ${q(node).property('_identifier')}

    honeypotScript = afx`
        <script data-name={"field_" + q(node).property('_identifier')} data-value={PaperTiger.timestampWithHmac()}>
            {StaticResource.content('Sitegeist.PaperTiger', 'Private/Templates/Honeypot.js')}
        </script>
    `

    renderer = Neos.Fusion:Case {
        backend {
            condition = ${node.context.inBackend}
            renderer = afx`
                <Sitegeist.PaperTiger:FieldContainer
                    field.name={props.name}
                >
                    <label>Honeypot</label>
                </Sitegeist.PaperTiger:FieldContainer>
            `
        }

        default {
            condition = true
            renderer = afx`
                <Neos.Fusion.Form:FieldContainer
                    attributes.style="display:none !important;"
                    attributes.autocomplete="off"
                    attributes.tabindex="-1"
                >
                    <Neos.Fusion.Form:Input field.name={props.name + '[one]'} attributes.value="" />
                    <Neos.Fusion.Form:Input field.name={props.name + '[two]'} attributes.value={PaperTiger.timestampWithHmac()} />
                    <Neos.Fusion.Form:Input field.name={props.name + '[three]'} attributes.id={'field_' + props.name} attributes.value="" />
                    {props.honeypotScript}
                </Neos.Fusion.Form:FieldContainer>
            `
        }
    }
}

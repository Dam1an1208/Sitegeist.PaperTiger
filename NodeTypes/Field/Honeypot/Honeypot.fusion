prototype(Sitegeist.PaperTiger:Field.Honeypot) < prototype(Neos.Neos:ContentComponent) {
    name = ${q(node).property('_identifier')}

    renderer = Neos.Fusion:Case {
        backend {
            condition = ${node.context.inBackend}
            renderer = afx`
                <Sitegeist.PaperTiger:FieldContainer
                    field.name={props.name}
                >
                    <label>Honeypot</label>
                </Sitegeist.PaperTiger:FieldContainer>
            `
        }

        default {
            condition = true
            renderer = afx`
                <Neos.Fusion.Form:FieldContainer
                    attributes.style="display:none !important;"
                    attributes.autocomplete="off"
                    attributes.tabindex="-1"
                >
                    <Neos.Fusion.Form:Input field.name={props.name + '[one]'} attributes.value="" />
                    <Neos.Fusion.Form:Input field.name={props.name + '[two]'} attributes.value={PaperTiger.timestampWithHmac()} />
                    <Neos.Fusion.Form:Input field.name={props.name + '[three]'} attributes.id={'field_' + props.name} attributes.value="" />
                    <script data-name={"field_" + props.name} data-value={PaperTiger.timestampWithHmac()}>
                        {'
                            const events = ["touchstart", "keydown", "mousemove", "touchmove"];
                            const dataset = document.currentScript.dataset;

                            function insertSpamProtectionValue() {
                                const field = document.getElementById(dataset.name);
                                const value = dataset.value;
                                if (field && value) {
                                    field.setAttribute("value", value);
                                }
                                removeEventListeners();
                            }

                            function addEventListeners() {
                                events.forEach(event => {
                                    document.addEventListener(event, insertSpamProtectionValue, true);
                                });
                            }

                            function removeEventListeners() {
                                events.forEach(event => {
                                    document.removeEventListener(event, insertSpamProtectionValue, true);
                                });
                            }

                            addEventListeners();
                        '}
                    </script>
                </Neos.Fusion.Form:FieldContainer>
            `
        }
    }
}

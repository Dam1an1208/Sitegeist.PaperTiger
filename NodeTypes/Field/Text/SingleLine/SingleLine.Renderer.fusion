prototype(Sitegeist.PaperTiger:Field.Text.SingleLine.Renderer) < prototype(Neos.Fusion:Component) {
  identifier = ${q(node).property('identifier')}
  label = ${q(node).property('label')}
  placeholder = ${q(node).property('placeholder')}
  isRequired = ${q(node).property('isRequired')}
  minimumLength = ${q(node).property('minimumLength')}
  maximumLength = ${q(node).property('maximumLength')}
  regularExpression = ${q(node).property('regularExpression')}

  renderer = afx`
    <Sitegeist.PaperTiger:FieldContainer
      field.name={props.identifier}
      label={props.label}
    >
      <Neos.Fusion.Form:Input
        attributes.required={props.isRequired || false}
        attributes.minlength={props.minimumLength || false}
        attributes.maxlength={props.maximumLength || false}
        attributes.pattern={props.regularExpression || false}
        attributes.placeholder={props.placeholder || false}
      />
    </Sitegeist.PaperTiger:FieldContainer>
  `
}

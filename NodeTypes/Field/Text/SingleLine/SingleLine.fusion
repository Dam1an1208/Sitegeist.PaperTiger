prototype(Sitegeist.PaperTiger:Field.Text.SingleLine) < prototype(Neos.Neos:ContentComponent) {
  name = ${q(node).property('name')}
  label = ${q(node).property('label')}
  placeholder = ${q(node).property('placeholder')}
  isRequired = ${q(node).property('isRequired')}
  minimumLength = ${q(node).property('minimumLength')}
  maximumLength = ${q(node).property('maximumLength')}
  regularExpression = ${q(node).property('regularExpression')}
  customErrorMessageEnabled = ${q(node).property('customErrorMessageEnabled')}
  customErrorMessage = ${q(node).property('customErrorMessage')}

  renderer = afx`
    <Sitegeist.PaperTiger:FieldContainer
      field.name={props.name}
      label={props.label}
    >
      <Neos.Fusion.Form:Input
        attributes.required={props.isRequired || false}
        attributes.minlength={props.minimumLength || false}
        attributes.maxlength={props.maximumLength || false}
        attributes.pattern={props.regularExpression || false}
        attributes.placeholder={props.placeholder || false}
        attributes.oninvalid={"this.setCustomValidity('" + props.customErrorMessage + "')"}
        attributes.oninvalid.@if={props.customErrorMessageEnabled}
        attributes.oninput={"this.setCustomValidity('')"}
        attributes.oninput.@if={props.customErrorMessageEnabled}
      />
    </Sitegeist.PaperTiger:FieldContainer>
  `
}




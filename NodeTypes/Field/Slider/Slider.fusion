prototype(Sitegeist.PaperTiger:Field.Slider) < prototype(Neos.Neos:ContentComponent) {
  identifier = ${q(node).property('identifier')}
  label = Sitegeist.PaperTiger:Lable.Editable
  placeholder = ${q(node).property('placeholder')}
  isRequired = ${q(node).property('isRequired')}
  minimumValue = ${q(node).property('minimumValue')}
  maximumValue = ${q(node).property('maximumValue')}
  stepValue = ${q(node).property('stepValue')}

  renderer = afx`
    <Sitegeist.PaperTiger:FieldContainer
        field.name={props.identifier}
        label={props.label}
    >
      <Neos.Fusion.Form:Input
        attributes.type="range"
        attributes.required={props.isRequired}
        attributes.placeholder={props.placeholder || false}
        attributes.min={Type.isInteger(props.minimumValue) ? props.minimumValue : false}
        attributes.max={Type.isInteger(props.maximumValue) ? props.maximumValue : false}
        attributes.step={Type.isInteger(props.stepValue) ? props.stepValue : false}
      />
    </Sitegeist.PaperTiger:FieldContainer>
  `
}

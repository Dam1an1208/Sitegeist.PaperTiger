prototype(Sitegeist.PaperTiger:FieldContainerRenderer) < prototype(Neos.Fusion.Form:Component.Field) {
  label = null
  attributes = Neos.Fusion:DataStructure
  content = null

  class = ${Configuration.setting('Sitegeist.PaperTiger.rendering.classes.container')}
  errorClass = ${Configuration.setting('Sitegeist.PaperTiger.rendering.classes.error')}

  containerRenderer = ${Configuration.setting('Sitegeist.PaperTiger.rendering.prototypes.container')}
  labelRenderer = ${Configuration.setting('Sitegeist.PaperTiger.rendering.prototypes.label')}
  errorRenderer = ${Configuration.setting('Sitegeist.PaperTiger.rendering.prototypes.error')}

  renderer = Neos.Fusion:Case {
    custom {
      condition = Neos.Fusion:CanRender {
        type = ${props.containerRenderer}
      }
      renderer = Neos.Fusion:Renderer {
        type = ${props.containerRenderer}
        element {
          label = ${props.label}
          attributes = ${props.attributes}
          field = ${props.field}
          content = ${props.content}
          class = ${props.class}
          errorClass = ${props.errorClass}
          labelRenderer = ${props.labelRenderer}
          errorRenderer = ${props.errorRenderer}
        }
      }
    }
    default {
      condition = true
      renderer = Neos.Fusion.Form:FieldContainer {
        label = ${props.label}
        attributes = ${props.attributes}
        field = ${props.field}
        content = ${props.content}
        class = ${props.class}
        errorClass = ${props.errorClass}
        labelRenderer = ${props.labelRenderer}
        errorRenderer = ${props.errorRenderer}
      }
    }
  }
}

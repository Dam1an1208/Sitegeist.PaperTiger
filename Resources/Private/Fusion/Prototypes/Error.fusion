prototype(Sitegeist.PaperTiger:Error) < prototype(Neos.Fusion:Component) {
  customErrorMessage = ${q(node).property('customErrorMessage')}
  customErrorMessageEnabled = ${q(node).property('customErrorMessageEnabled')}

  result = null

  renderer = Neos.Fusion:Case {

    override {
      condition = Neos.Fusion:CanRender {
        type = 'Sitegeist.PaperTiger:Error.Override'
      }
      renderer = Sitegeist.PaperTiger:Error.Override {
        @apply.props = ${props}
      }
    }

    customErrorMessage {
      condition = ${props.customErrorMessageEnabled && props.customErrorMessage}
      renderer = afx`
        <div class={Configuration.setting("Sitegeist.PaperTiger.rendering.classes.customError")}>
          {props.customErrorMessage}
        </div>
      `
    }

    default {
      condition = true
      renderer = Neos.Fusion.Form:ErrorRenderer {
        result = ${props.result}
      }
    }
  }
}


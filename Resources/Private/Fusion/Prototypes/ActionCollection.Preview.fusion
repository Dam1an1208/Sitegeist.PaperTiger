prototype(Sitegeist.PaperTiger:ActionCollection.Preview) < prototype(Neos.Neos:ContentCollection) {
  nodePath = null
  prototype(Neos.Neos:ContentCollectionRenderer) {
    itemRenderer = Sitegeist.PaperTiger:ActionCollection.Preview.ContentCase
  }
}

prototype(Sitegeist.PaperTiger:ActionCollection.Preview.ContentCase) < prototype(Neos.Neos:ContentComponent) {
  label = ${node.label}
  preview = Neos.Fusion:Case {
    override {
      @position = 'before preview'
      condition = Neos.Fusion:CanRender {
        type = ${node.nodeType.name + '.Preview.Override'}
      }
      type = ${node.nodeType.name + '.Preview.Override'}
    }

    preview {
      @position = 'end'
      condition = true
      type = ${node.nodeType.name + '.Preview'}
    }
  }

  renderer = afx`
    <dl>
      <dt>{props.label}</dt>
      <dd>{props.preview}</dd>
    </dl>
  `
}
